import pandas as pd


def genome_base(genome):
    base = {
        "rn6": "GCF_000001895.5_Rnor_6.0_genomic",
        "rn7": "GCF_015227675.2_mRatBN7.2_genomic"
    }
    return base[genome]


def fastq_paths(wildcards):
    files = fastqs.loc[fastqs["library"] == wildcards.sample_id]
    files = files.to_dict(orient="records")
    return ["../br/data/fastq/batch{}/{}".format(x["batch"], x["filename"]) for x in files]


fastqs = pd.read_csv("../br/data/fastq/fastq_files_listing.fixed.txt", sep="\t")
samples = list(pd.read_csv("samples.txt", sep="\t", names=["sample"])["sample"])

localrules:
    chr_counts_all,


rule all:
    input:
        # expand("data/star_index_{genome}/SAindex", genome=["rn6", "rn7"]),
        # expand("data/star_out_{genome}/{sample_id}.Aligned.sortedByCoord.out.bam",
        #        genome=["rn6", "rn7"], sample_id=samples),
        # expand("data/read_chrs_{genome}/{sample_id}.txt.gz", genome=["rn6", "rn7"], sample_id=samples),
        "chr_counts.txt"


rule star_index:
    input:
        fasta = lambda w: f"data/{genome_base(w.genome)}.fna",
        gtf = lambda w: f"data/{genome_base(w.genome)}.gtf"
    output:
        # Among others:
        "data/star_index_{genome}/SAindex"
    params:
        outdir = "data/star_index_{genome}"
    threads: 8
    shell:
        """
        STAR --runMode genomeGenerate \
            --genomeDir {params.outdir} \
            --genomeFastaFiles {input.fasta} \
            --sjdbGTFfile {input.gtf} \
            --sjdbOverhang 99 \
            --runThreadN 8
        """


rule star_align:
    input:
        fastq = fastq_paths,
        index = "data/star_index_{genome}/SAindex"
    output:
        "data/star_out_{genome}/{sample_id}.Aligned.sortedByCoord.out.bam"
    params:
        fastq_list = lambda wildcards, input: ",".join(input.fastq),
        index_dir = "data/star_index_{genome}",
        prefix = "data/star_out_{genome}/{sample_id}.",
    resources:
        mem_mb = 60000,
        cpus = 16
    shell:
        """
        STAR --runMode alignReads \
            --runThreadN 16 \
            --genomeDir {params.index_dir} \
            --readFilesIn {params.fastq_list} \
            --readFilesCommand zcat \
            --outSAMtype BAM SortedByCoordinate \
            --outSAMunmapped Within \
            --outFileNamePrefix {params.prefix}
        """


rule read_chrs:
    input:
        "data/star_out_{genome}/{sample_id}.Aligned.sortedByCoord.out.bam"
    output:
        "data/read_chrs_{genome}/{sample_id}.txt.gz"
    shell:
        "samtools view {input} | cut -f1,3 | gzip -c > {output}"


rule chr_counts_sample:
    input:
        expand("data/read_chrs_{genome}/{{sample_id}}.txt.gz", genome=["rn6", "rn7"])
    output:
        "data/chr_counts/{sample_id}.txt"
    shell:
        "Rscript chr_counts.R {wildcards.sample_id} {output}"


rule chr_counts_all:
    input:
        expand("data/chr_counts/{sample_id}.txt", sample_id=samples)
    output:
        "chr_counts.txt"
    shell:
        "cat {input} > {output}"
